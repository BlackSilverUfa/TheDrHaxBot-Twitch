(

$positive := [
    'определённо да OkayChamping',
    'можешь быть уверен в этом OkayChamping',
    'никаких сомнений OkayChamping',
    'абсолютли OkayChamping'
];

$plausible := [
    'хорошие шансы SeemsGood',
    'вероятнее всего SeemsGood',
    'думаю, что да SeemsGood',
    'не исключено SeemsGood',
    'немаловероятно SeemsGood'
];

$uncertain := [
    'пока не ясно peepoThink',
    'не уверен peepoThink',
    'тут всё не так однозначно peepoThink',
    'скажу за небольшую сумму, BSUDAI',
    'не стоит вскрывать эту тему. Вы молодые, шутливые, вам все легко. Это не то monkaGunshake'
];

$unplausible := [
    'да нет, наверное BSUHee',
    'шансы не очень BSUHee',
    'скорее нет, чем да BSUHee',
    'я бы не надеялся BSUHee'
];

$negative := [
    'даже не надейся WeirdChamping',
    'весьма сомнительно WeirdChamping',
    'определённо нет WeirdChamping',
    'вероятность КРАЙНЕ мала WeirdChamping'
];

$choose := function ($l) {(
    $id := $floor($random() * $count($l));
    $l[$id]
)};

(parsed.query ~> $match(/ (или|or) /)) ? (
    $options := parsed.query
        ~> $substringBefore('?') ~> $replace(/ (или|or) /, ', ')
        ~> $split(',') ~> $map(function ($i) { $trim($i) });

    ($count($options) = 2 and 'не ' & $options[0] = $options[1]) ? 'вот в чём вопрос Kojimaptyp' :
    ($count($options) = 2 and $options[0] = $options[1]) ? 'да OkayChamping' :
    (
        $option := $choose($options);
        
        $choose([
            'скорее всего ' & $option & ' peepoThink',
            'определённо ' & $option & ' OkayChamping',
            $option & ' — наш выбор SeemsGood'
        ])
    )
) :
(parsed.query_filtered ~> $match(/от -?[0-9]+ до -?[0-9]+/)) ? (
    $range := (parsed.query_filtered ~> $match(/от (-?[0-9]+) до (-?[0-9]+)/)).groups
        ~> $map(function ($v) { $number($v) }) ~> $sort;
    
    $choice := $floor($random() * ($range[1] - $range[0] + 1) + $range[0]);
    
    $choose([
        'определённо ' & $choice & ' OkayChamping',
        'скорее всего ' & $choice & ' peepoThink',
        $choice & ' — наш выбор SeemsGood'
    ])
) :
(parsed.query_filtered ~> $match(/^а+\.*$/i)) ? (
    $choose([
        '...ня ANYA',
        '...лло ANYA',
        '...да, я люблю тебя ANYA',
        '...ыаыа ANYA',
        '...уф 🐺',
        '...пчхи ANYA',
        '...яя AYAYA'
    ])
) :
(parsed.query_filtered ~> $match(/ты потрясающий/)) ? 'нет, ты потрясающий! keanU' :
(parsed.query_filtered ~> $match(/^[UO][wωv][UO]$/i)) ? $choose(['UωU', 'òωó', 'OωO', 'ಠ_ಠ']) :
(parsed.query_filtered ~> $match(/^(PETTHE|(не )?пип(ай|ни|\ |$))/)) ? 'пип ' & $choose([
    'PETTHEPEEPO',
    'PETTHEAPTEEPO',
    'PETTHECAT',
    'PETTHEEVAN',
    'PETTHEMOD'
]) :
(
    $choose([
        $choose($positive),
        $choose($plausible),
        $choose($uncertain),
        $choose($unplausible),
        $choose($negative)
    ])
)

)
