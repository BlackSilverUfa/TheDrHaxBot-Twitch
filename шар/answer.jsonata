(

$positive := [
    'определённо да YEPPERS',
    'можешь быть уверен в этом YEPPERS',
    'никаких сомнений YEPPERS',
    'абсолютли YEPPERS',
    'ну естественно YEPPERS',
    'да YEPPERS'
];

$plausible := [
    'хорошие шансы SeemsGood',
    'вероятнее всего SeemsGood',
    'думаю, что да SeemsGood',
    'не исключено SeemsGood',
    'немаловероятно SeemsGood'
];

$uncertain := [
    'пока не ясно peepoThink',
    'не уверен peepoThink',
    'тут всё не так однозначно peepoThink',
    'скажу за небольшую сумму, BSUDAI',
    'не стоит вскрывать эту тему. Вы молодые, шутливые, вам все легко. Это не то. Погибнут ВСЁ monkaGunshake'
];

$unplausible := [
    'да нет, наверное BSUHee',
    'шансы не очень BSUHee',
    'скорее нет, чем да BSUHee',
    'я бы не надеялся BSUHee'
];

$negative := [
    'даже не надейся NOPERS',
    'весьма сомнительно NOPERS',
    'определённо нет NOPERS',
    'вероятность КРАЙНЕ мала NOPERS',
    'никогда в жизни NOPERS',
    'нет, просто нет NOPERS'
];

$choose := function ($l) {(
    $id := $floor($random() * $count($l));
    $l[$id]
)};

$continuations := [
    'аня ANYA',
    'алло ANYA',
    'ада, я люблю тебя ANYA',
    'аыаыа ANYA',
    'ауф 🐺',
    'апчхи ANYA',
    'аяя AYAYA',
    'ага YEPPERS',
    'ass gachiBASS',
    'ававава MEGALUL',
    
    'мифа BibleThump',
    
    'поползень popCat',
    'попа peepoGiggles',
    'попола AYAYA',
    'девола AYAYA',
    
    'куля-буля ANYA',
    'куляля ANYA',
    'куку, ипта ANYA',
    'кукусики NomNom'
];

$translate := function($text, $from, $to) {
    $text
        ~> $split('')
        ~> $map(function($x) {(
            $index := $from
                ~> $split('')
                ~> $map(function($y, $i) {
                    $x = $y ? $i
                })[0];
            
            $index != null ? $to[$index] : $x
        )})
        ~> $join()
};

$startsWith := function ($x, $y) {(
    $x := $substring($x, 0, $length($y))
        ~> $translate('a', 'а');
    $y := $y
        ~> $translate('a', 'а');
    $x = $y
)};

$continue := function($text) {(
    $text := $text
        ~> $lowercase
        ~> $replace(/\.*$/, '');

    $candidates := $continuations ~> $map(function($x) {
        $startsWith($x, $text) ? (
            $substring($x, $length($text))
        ) : null
    }) ~> $filter(function($x) { $x }) ~> $choose
)};

(parsed.query ~> $length = 0) ? (
    'чт' & (
        $match(payload.message, /!ш+([ао]+)р+/i).groups[0]
        ~> $replace('а', 'o') ~> $replace('А', 'O')
        ~> $replace('о', 'а') ~> $replace('О', 'А')
        ~> $replace('o', 'о') ~> $replace('O', 'О')
    ) & '? Jebaited'
) :
(parsed.query ~> $match(/ (или|or) /)) ? (
    $options := parsed.query
        ~> $substringBefore('?') ~> $replace(/ (или|or) /, ', ')
        ~> $split(',') ~> $map(function ($i) { $trim($i) });

    ($count($options) = 2 and 'не ' & $options[0] = $options[1]) ? 'вот в чём вопрос Kojimaptyp' :
    ($count($options) = 2 and 'not ' & $options[0] = $options[1]) ? 'that is the question Kojimaptyp' :
    ($count($options) = 2 and $options[0] = $options[1]) ? 'да YEPPERS' :
    (
        $option := $choose($options);
        
        $choose([
            'скорее всего ' & $option & ' peepoThink',
            'определённо ' & $option & ' YEPPERS',
            $option & ' — наш выбор SeemsGood'
        ])
    )
) :
(parsed.query ~> $match(/от [+-]?[0-9]+ до [+-]?[0-9]+/)) ? (
    $range := (parsed.query_filtered ~> $match(/от ([+-]?[0-9]+) до ([+-]?[0-9]+)/)).groups
        ~> $map(function ($v) { $number($v ~> $replace(/\+/, '')) }) ~> $sort;
    
    $choice := $floor($random() * ($range[1] - $range[0] + 1) + $range[0]);
    
    $choose([
        'определённо ' & $choice & ' YEPPERS',
        'скорее всего ' & $choice & ' peepoThink',
        $choice & ' — наш выбор SeemsGood'
    ])
) :
(parsed.query_filtered ~> $continue) ? (
    '...' & $continue(parsed.query_filtered)
) :
(parsed.query_filtered ~> $match(/^пинг/i)) ? (
    "понг Pepega"
) :
(parsed.query ~> $match(/^может чайку/)) ? 'а может тебя?! Jebaited' :
(parsed.query ~> $match(/^ты потрясающий/)) ? 'нет, ты потрясающий! keanU' :
(parsed.query ~> $match(/^[UO][wωv][UO]$/i)) ? $choose(['UωU', 'òωó', 'OωO', 'ಠ_ಠ']) :
(parsed.query ~> $match(/^(PETTHE|(не )?пип(ай|ни|\ |$))/)) ? 'пип ' & $choose([
    'PETTHEPEEPO',
    'PETTHEAPTEEPO',
    'PETTHECAT',
    'PETTHEEVAN',
    'PETTHEMOD'
]) :
(
    $choose([
        $choose($positive),
        $choose($plausible),
        $choose($uncertain),
        $choose($unplausible),
        $choose($negative)
    ])
)

)
